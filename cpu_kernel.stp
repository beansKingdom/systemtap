#!/usr/bin/stap

global offcpubts,oncpubts
global start_time

probe begin {
    warn(sprintf("Tracing %d in kernel-space only...\n", target()))
}

probe timer.profile {
    if (pid() == target() && !user_mode()) {
        oncpubts[backtrace()] <<< 1;
    }
}

probe scheduler.cpu_off {
    if (pid() == target()) {
        start_time[tid()] = gettimeofday_us()
    }
}

probe scheduler.cpu_on {
    if (pid() == target()) {
        t = tid()
        begin = start_time[t]
        if (begin > 0) {
            elapsed = gettimeofday_us() - begin
            if (elapsed >= 1) {
                offcpubts[backtrace()] <<< elapsed
            }
            delete start_time[t]
        }
    }
}

probe timer.s(10) {
exit()
}

probe end {
    printf("-----oncpu--------")
    foreach ([ubt] in oncpubts- limit 10240) {
        print_stack(ubt)
        printf("\t%d\n", @count(oncpubts[ubt]));
    }
    printf("-----offcpu--------")
    foreach ([ubt] in offcpubts- limit 10240) {
        print_stack(ubt)
        printf("\t%d\n", @count(offcpubts[ubt]))
    }

}
